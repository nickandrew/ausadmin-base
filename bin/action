#!/usr/bin/perl
#	@(#) action: Do the "next action" for a proposal
#	Usage: action proposal-name
#
# $Source$
# $Revision$
# $Date$
#

=head1 NAME

action - Do the "next action" for a proposal

=head1 SYNOPSIS

cd ~ausadmin
action proposal-name [vote_dir]

=head1 DESCRIPTION

TODO

=cut

use Time::Local;
use lib 'perllib';
use Ausadmin qw();
use DateFunc qw();
use Vote qw();

$| = 1;

my $rfd_min_days = 21;

my $state_actions = {
	'new/norfd' => \&create_rfd,
	'rfd/posted' => \&check_rfd,
	'rfd/unposted' => \&post_rfd,
	'rfd/unsigned' => \&sign_rfd,
	'complete/result-wait' => \&pause_result,
	'complete/resultnotsigned' => \&sign_result,
	'complete/resultnotposted' => \&post_result,
	'complete/pass' => \&group_finished,
	'complete/pass/unprocessed' => \&make_control,
	'complete/pass/unsigned' => \&sign_control,
	'complete/pass/signed' => \&post_one_control,
	'vote/checking' => \&gen_result,
	'vote/running' => \&pause_running,
	'vote/cfvnotposted' => \&post_cfv,
	'vote/cfvnotsigned' => \&sign_cfv,
	'vote/nocfv' => \&gen_cfv,
	'vote/notsetup' => \&setup_vote,
	'vote/novoterule' => \&gen_voterule,
};

my $newsgroup = shift @ARGV || die "Usage: action newsgroup-name\n";
my $vote_dir = shift @ARGV || './vote';

die "No $vote_dir directory (must cd to ~ausadmin)" if (!-d $vote_dir);

my $v = new Vote(name => $newsgroup, vote_dir => $vote_dir);
my $ng_dir = $v->ng_dir();

die "No $ng_dir" if (!-d $ng_dir);

my $s = 0;

foreach my $f (qw/distribution proposer rationale/) {
	if (!-f "$ng_dir/$f") {
		print STDERR "No file $ng_dir/$f ...\n";
		$s = 8;
	}
}

if ($s) {
	die "Some RFD files missing, setup the directory with new-rfd first!";
}

# Compute the current vote state
my $state = $v->state();

# Loop through until we are unable to process anything more

while (exists $state_actions->{$state}) {
	my $code_ref = $state_actions->{$state};

	print "state = $state\n";

	my $rc;
	eval {
		$rc = &$code_ref($v);
	};

	if ($@) {
		print "Unable to process state=$state: $@\n";
		last;
	}

	last if ($rc);

	my $new_state = $v->state();
	if ($new_state eq $state) {
		print "New state is same as old state, $state, so leaving\n";
		last;
	}
	$state = $new_state;
}

print "Exit at state $state\n";

if (!exists $state_actions->{$state}) {
	die "Not prepared to handle state $state\n";
}

exit(0);

sub gen_result {
	# Generate a result message
	print "(state=$state) Generate result.unsigned ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("genresult.pl $newsgroup > $ng_dir/result.unsigned");
	$v->audit("generated result (code $rc)");
	if ($rc) {
		unlink("$ng_dir/result.unsigned");
		die "genresult.pl failed code $rc, unlinked result.unsigned ...!";
	}

	print "\nOkay.\n";

	return 0;
}

sub sign_result {
	# Generate a result message
	print "(state=$state) Sign result.unsigned ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("pgp-sign < $ng_dir/result.unsigned > $ng_dir/result");
	$v->audit("PGP-signed result (code $rc)");
	if ($rc) {
		unlink("$ng_dir/result");
		die "pgp-sign failed code $rc, unlinked result ...!";
	}

	print "\nOkay.\n";

	return 0;
}

sub gen_voterule {
	if (-f "$ng_dir/voterule") {
		die "Augh. gen_voterule prerequisites not met!";
	}
	print "(state=$state) Create voterule ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $vote_rule = "$ng_dir/voterule";
	if (!-f "config/voterule") {
		die "unable to create voterule: config/voterule does not exist!";
	}

	open(F, "<config/voterule");
	my $vr = <F>;
	close(F);
	open(G, ">$vote_rule");
	print G $vr;
	close(G);

	$v->audit("created voterule");
	print "\nOkay.\n";

	return 0;
}

sub setup_vote {
	if (!-f "$ng_dir/voterule" || -f "$ng_dir/endtime.cfg") {
		die "Augh. setup_vote prerequisites not met!";
	}

	print "(state=$state) Start to accept votes now ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	$v->setup_vote();

	print "\nOkay.\n";

	return 0;
}

sub gen_cfv {
	if (!-f "$ng_dir/voterule" || !-f "$ng_dir/endtime.cfg" || -f "$ng_dir/cfv") {
		die "Augh. gen_cfv prerequisites not met!";
	}

	print "(state=$state) Create CFV ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("mkcfv.pl $newsgroup > $ng_dir/cfv");
	$v->audit("created CFV (code $rc)");
	if ($rc) {
		unlink("$ng_dir/cfv");
		die "mkcfv.pl failed code $rc, unlinked cfv ...!";
	}

	print "\nOkay.\n";

	return 0;
}

sub sign_cfv {
	if (!-f "$ng_dir/cfv" || -f "$ng_dir/cfv.signed") {
		die "Augh. sign_cfv prerequisites not met!";
	}

	print "(state=$state) PGP-sign CFV ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("pgp-sign < $ng_dir/cfv > $ng_dir/cfv.signed");
	$v->audit("signed CFV (code $rc)");
	if ($rc) {
		unlink("$ng_dir/cfv.signed");
		die "pgp-sign failed code $rc, unlinked cfv.signed ...!";
	}

	print "\nOkay.\n";

	return 0;
}

sub post_cfv {
	if (!-f "$ng_dir/cfv.signed" || !-f "$ng_dir/endtime.cfg" || -f "$ng_dir/posted.cfg") {
		die "Augh. post_cfv prerequisites not met!";
	}

	print "(state=$state) Post the signed CFV ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("post.pl < $ng_dir/cfv.signed");
	$v->audit("Posted CFV (code $rc)");
	if ($rc) {
		die "post failed code $rc...!";
	}

	my($sec,$min,$hour,$mday,$mon,$year) = localtime(time());
	$mon++; $year += 1900;
	my $yyyymmdd = sprintf "%d-%02d-%02d", $year,$mon,$mday;
	open(F, ">$ng_dir/posted.cfg");
	print F $yyyymmdd, "\n";
	close(F);

	print "\nOkay. Nothing more to do until vote is finished.\n";

	return 0;
}

sub post_result {
	if (!-f "$ng_dir/result") {
		die "Augh. post_result prerequisites not met!";
	}

	print "(state=$state) Post the result ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("post.pl < $ng_dir/result");
	$v->audit("posted result (code $rc)");
	if ($rc) {
		die "post failed code $rc...!";
	}

	my($sec,$min,$hour,$mday,$mon,$year) = localtime(time());
	$mon++; $year += 1900;
	my $yyyymmdd = sprintf "%d-%02d-%02d", $year,$mon,$mday;
	open(F, ">$ng_dir/result_posted.cfg");
	print F $yyyymmdd, "\n";
	close(F);

	print "\nOkay.\n";

	return 0;
}

sub create_rfd {
	my $v = shift;

	# Get the rfd_posted.cfg (yyyy-mm-dd)
	if (-e "$ng_dir/rfd") {
		die "Augh. Did not expect $ng_dir/rfd to exist!";
	}

	if (-e "$ng_dir/rfd.unsigned") {
		die "Augh. Did not expect $ng_dir/rfd.unsigned to exist!";
	}

	# Otherwise, ask
	print "(state=$state) Create RFD file [y,n] ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^y$/i) {
		die "Aborting\n";
	}

	if ($answer =~ /y/i) {
		print "Ok, creating the RFD now ...\n";
		$v->create_rfd();
		print "Done! Check it for errors then run action again.\n";
		# User probably wants to look at the file before continuing.
		return 16;
	}
}

sub check_rfd {
	my $v = shift;

	# Get the rfd_posted.cfg (yyyy-mm-dd)
	if (!-f "$ng_dir/rfd_posted.cfg") {
		die "Augh. expected rfd_posted.cfg";
	}

	open(R, "<$ng_dir/rfd_posted.cfg") or die "Unable to open rfd_posted.cfg: $!";
	my $rfd_posted = <R>;
	chomp($rfd_posted);
	close(R);

	my $yyyymmdd = Ausadmin::today();

	if (DateFunc::days_between($rfd_posted, $yyyymmdd) < $rfd_min_days) {
		print "RFD for $newsgroup was posted on $rfd_posted, not old enough for a decision\n";
		return 16;	# don't go on with this group
	}

	my $recip = $v->read_file('proposer');
	$recip = $recip->[0];

	$recip =~ /<(.*)>/;
	if (! $1) {
		die "No parseable recipient address $recip";
	}
	$recip = $1;
	# Unmunge it
	$recip =~ s/\.\././g;
	$recip =~ s/ at /\@/g;

	print "Proposer is $recip\n";

	# Otherwise, ask
	print "(state=$state) Abandon RFD, send mail or setup Vote [a,m,v] ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^[amv]$/i) {
		die "Aborting\n";
	}

	if ($answer =~ /a/i) {
		$v->abandon();

		print "Okay, I abandoned the RFD.\n";
		return 16;
	}

	if ($answer =~ /m/i) {
		print "Sending message to $recip ...\n";

		my $hr = {
			NG => $newsgroup,
			RECIP => $recip,
		};

		Ausadmin::sendmail_template("$ENV{AUSADMIN_HOME}/config/vote-confirm.template", $hr, $recip);

		print "Message sent! Wait for a reply\n";

		return 8;
	}

	if ($answer =~ /v/i) {
		print "Setting up vote now ...\n";
		$v->setup_vote();
		return 0;
	}

	return 8;
}

sub sign_rfd {
	# Sign the RFD
	print "(state=$state) Sign rfd.unsigned ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("pgp-sign < $ng_dir/rfd.unsigned > $ng_dir/rfd");
	$v->audit("PGP-signed RFD (code $rc)");
	if ($rc) {
		unlink("$ng_dir/rfd");
		die "pgp-sign failed code $rc, unlinked rfd ...!";
	}

	print "\nOkay.\n";

	return 0;
}

sub post_rfd {
	# Signed RFD exists, post it?
	if (-f "$ng_dir/rfd_posted.cfg") {
		die "Augh. Did not expect rfd_posted.cfg";
	}

	if (! -f "$ng_dir/rfd") {
		die "Augh. Expected signed rfd in $ng_dir/rfd";
	}

	# Otherwise, ask
	print "(state=$state) Post RFD ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer =~ /^y/i) {
		$v->post_rfd();

		print "Okay, I posted it. Nothing more to do for 21 days.\n";
		exit(0);
	}

	# Otherwise, abort
	die "Ok, try again later.\n";
}

sub pause_running {
	print "(state=$state) Nothing to do, vote is currently running.\n";
	exit(0);
}

sub pause_result {
	print "(state=$state) Result is being discussed, wait.\n";
	exit(0);
}

sub sign_control {
	# Sign the control message
	print "(state=$state) Sign control.msg ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("signcontrol < $ng_dir/control.msg > $ng_dir/control.signed");
	$v->audit("PGP-signed control message (code $rc)");
	if ($rc) {
		unlink("$ng_dir/control.signed");
		die "pgp-sign failed code $rc, unlinked control.signed ...!";
	}

	print "\nOkay.\n";

	return 0;
}

#  Create appropriate control messages (use perform-changes.pl to create
#  messages which do whatever the 'change' file requires).

sub make_control {
	print "(state=$state) Create control message(s) ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("perform-changes.pl $newsgroup");
	$v->audit("Executed perform-changes.pl (code $rc)");
	if ($rc) {
		die "perform-changes.pl failed code $rc...!";
	}

	print "\nOkay.\n";

	return 0;
}

#  Post the control.signed file to make a newsgroup change effective.
#  Note: this function assumes there is only one control file to post,
#  that is not the case with multi-group changes ...!

sub post_one_control {
	print "(state=$state) Post (one) control message ? ";
	my $answer = <STDIN>;
	chomp($answer);
	if ($answer !~ /^\s*y/i) {
		die "Ok, try again later.\n";
	}

	my $rc = system("post.pl < $ng_dir/control.signed");
	$v->audit("Posted control.signed (code $rc)");
	if ($rc) {
		die "post.pl failed code $rc...!";
	}

	# Now create group.creation.date, containing today's date

	my $yyyymmdd = Ausadmin::today();
	$rc = write1line("$ng_dir/group.creation.date", "$yyyymmdd\n");
	$v->audit("Wrote group.creation.date (code $rc)");
	if ($rc) {
		die "write1line failed code $rc...!";
	}

}

sub write1line {
	my $path = shift;
	my $string = shift;

	if (!open(ONELINE, ">$path")) {
		return -1;
	}

	print ONELINE $string;

	if (!close(ONELINE)) {
		return -2;
	}

	return 0;
}

sub group_finished {
	print "This newsgroup is now complete (exiting),\n";

	exit(0);
}
